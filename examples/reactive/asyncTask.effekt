module reactive/asyncTask
import immutable/option
import reactive/types
import reactive/effects

def asyncTask[T]{ task: => T / Async }: T / {Scheduling} = {
    // Stage 1: Initialize process
    var p = fun(){()} 
    var r = None()
    // Stage 2: try task
    p = fun(){ 
        try {
            r = Some(task())
        } with Async {
                              // Stage 3: Await
            def await(poll) = {
                var temp = fun(){()}
                temp = fun() {
                    poll() match {
                                       // Stay in Stage 3.
                        case None() => p = temp
                                       // Go back to Stage 2
                        case Some(v) => p = fun(){ resume(v) }
                    }
                }
                p = temp
            }
        }
    }
    
    def loop(): T = {
        p()
        r match {
            case Some(d) => d
            case None() => do tick(); loop()
        }
    }

    loop()
}
