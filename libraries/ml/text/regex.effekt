module text/regex

extern type Char // TODO move to text/string
extern type MLList[Elem] // = 'elem list
type Chars = MLList[Char]

extern pure def chars(str: String): Chars = "String.explode str"
extern pure def toString(chars: Chars): String = "String.implode chars"

extern """

(** The RegExp module is created by choosing a parser (P) and an engine (E)
  * for regular expressions.
  *
  * There is no big choice for the parser, since only Awk syntax is supported.
  * For the regular expression engine, we choose Ken Thompson's algorighm.
  *
  * Also see:
  *   <https://stackoverflow.com/questions/35462919/how-to-use-regex-in-smlnj>
  *   <https://www.smlnj.org/doc/smlnj-lib/RegExp/fun-RegExpFn.html>
  *)
structure RegExp = RegExpFn(
  structure P = AwkSyntax
  structure E = ThompsonEngine
  (* structure E = BackTrackEngine *)
  (* structure E = DfaEngine *)
)
"""

// The type of regular expressions (ML representation)
extern type Regex

record Match(matched: String)

// TODO handle CannotParse and CannotCompile ml exceptions.
extern pure def regex(regexString: String): Regex =
  "RegExp.compileString regexString"


// A stream of elements of type Elem, read from a source of type Source
extern type MLStream[Elem, Source] // = ('elem, 'source) StringCvt.reader

// Internal ml type that represents matches
//   <https://www.smlnj.org/doc/smlnj-lib/RegExp/str-MatchTree.html>
extern type MLMatchResult // = match * char list

extern """
type regex_match = {len: int, pos: char list} MatchTree.match_tree;
fun matchedString (match : regex_match * char list) =
  let val res = MatchTree.root (#1 match)
  in String.implode (List.take (#pos res, #len res)) end;

fun remainderChars (match : regex_match * char list) =
  let val res = MatchTree.root (#1 match)
  in List.drop (#pos res, #len res) end;
"""

extern pure def getMatch(result: MLMatchResult): String = "matchedString result"
extern pure def getRemainder(result: MLMatchResult): Chars = "remainderChars result"


extern pure def unsafeMatchPrefix(r: Regex, str: Chars): MLOption[MLMatchResult] =
  "RegExp.prefix r List.getItem str"

def exec(reg: Regex, str: String): Option[Match] = {
  val matched = reg.unsafeMatchPrefix(str.chars)
  if (matched.isDefined) { Some(Match(matched.force.getMatch())) }
  else { None() }
}